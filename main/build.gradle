apply plugin: 'com.android.feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion versions.build.compileSdk
    buildToolsVersion versions.build.tools

    defaultConfig {
        minSdkVersion versions.build.minSdk
        targetSdkVersion versions.build.targetSdk
        versionCode buildNumber
        versionName buildProperties.versionName

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        resConfigs 'en'

        // Setting this to true disables the png generation at buildtime
        // (see http://android-developers.blogspot.fr/2016/02/android-support-library-232.html)
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(buildProperties.signingStoreFile)
            storePassword buildProperties.signingStorePassword
            keyAlias buildProperties.signingKeyAlias
            keyPassword buildProperties.signingKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    // Disabled for now, see https://issuetracker.google.com/issues/63814741
//    dataBinding {
//        enabled = true
//    }

    lintOptions {
        abortOnError true
        textReport true
        ignoreWarnings true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    // Tests
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$versions.testing.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$versions.testing.junit"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    // Support library
    implementation "com.android.support:appcompat-v7:$versions.supportLibrary"

    implementation project(':base')

}

// See https://kotlinlang.org/docs/reference/kapt.html
kapt {
    correctErrorTypes = true
}